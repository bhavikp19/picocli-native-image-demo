buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        //classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.6.0"
        classpath "gradle.plugin.org.mikeneck:graalvm-native-image-plugin:0.2"
    }
}
plugins {
    id 'java'
}
//apply plugin: 'com.palantir.graal'
apply plugin: "org.mikeneck.graalvm-native-image"

group 'info.picocli'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'info.picocli:picocli:4.1.4'
    compile 'info.picocli:picocli-jansi-graalvm:1.1.0'
    compile 'org.fusesource.jansi:jansi:1.18'

    annotationProcessor 'info.picocli:picocli-codegen:4.1.4'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
}

compileJava {
    options.compilerArgs += [
            "-Aproject=${project.group}/${project.name}",
            "-Aother.resource.patterns=testkey.jks"
    ]
}

test {
    useJUnitPlatform()
}
test.dependsOn(tasks.getByName('nativeImage'))

// config for palantir
//graal {
//    mainClass 'picocli.nativeimage.demo.CheckSum'
//    outputName 'checksum'
//    graalVersion '19.3.0'
//    //option '--static'
//}

// config for org.mikeneck.graalvm-native-image
nativeImage {
    graalVmHome = System.getProperty('java.home')
    mainClass = 'picocli.nativeimage.demo.CheckSum'
    executableName = 'checksum'
    arguments(
            '--no-fallback',
//            '--enable-all-security-services',
//            '--initialize-at-run-time=com.example.runtime',
            '--report-unsupported-elements-at-runtime'
    )
}
// work around https://github.com/mike-neck/graalvm-native-image-plugin/issues/12
tasks.nativeImage.outputs.upToDateWhen { false }

